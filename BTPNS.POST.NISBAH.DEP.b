*-----------------------------------------------------------------------------
* <Rating>51</Rating>
*------------------------------------------------------------------------------
    SUBROUTINE BTPNS.POST.NISBAH.DEP
*-----------------------------------------------------------------------------
* Developer Name     : Hamka Ardyansah
* Development Date   : 27 September 2022
* Description        : ROutine to write changes nisbah to pooling table BTPNS.TH.POOL.NISBAH.DEP
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.AA.ACCOUNT
    $INSERT I_F.AA.ARRANGEMENT
    $INSERT I_F.AA.TERM.AMOUNT
    $INSERT I_F.AA.ACCOUNT.DETAILS
    $INSERT I_F.ACCOUNT
    $INSERT I_F.USER
    $INSERT I_F.AA.ACTIVITY.HISTORY
    $INSERT I_F.EB.CONTRACT.BALANCES
    $INSERT I_F.BTPNS.TH.POOL.NISBAH.DEP
    $INSERT I_AA.APP.COMMON
    $INSERT I_AA.LOCAL.COMMON
    $INSERT I_AA.ACTION.CONTEXT
    $INSERT I_GTS.COMMON
    
    Y.ACTIVITY.STATUS = c_arrActivityStatus
    
    IF Y.ACTIVITY.STATUS EQ "AUTH" THEN
        GOSUB INIT
        GOSUB PROCESS
    END


    RETURN

*------------------------------------------------------------------------------
INIT:
*------------------------------------------------------------------------------

    FN.AA.ARRANGEMENT   = 'F.AA.ARRANGEMENT'
    F.AA.ARRANGEMENT    = ''
    CALL OPF(FN.AA.ARRANGEMENT, F.AA.ARRANGEMENT)
        
    FN.AA.ACTIVITY.HISTORY  = 'F.AA.ACTIVITY.HISTORY'
    F.AA.ACTIVITY.HISTORY   = ''
    CALL OPF(FN.AA.ACTIVITY.HISTORY, F.AA.ACTIVITY.HISTORY)
    
    FN.AA.ARR.ACCOUNT = 'F.AA.ARR.ACCOUNT'
    F.AA.ARR.ACCOUNT = ''
    CALL OPF(FN.AA.ARR.ACCOUNT, F.AA.ARR.ACCOUNT)
    
    FN.ACCOUNT  = 'F.ACCOUNT'
    F.ACCOUNT   = ''
    CALL OPF(FN.ACCOUNT, F.ACCOUNT)
    
    FN.AA.ACCOUNT.DETAILS   = 'F.AA.ACCOUNT.DETAILS'
    F.AA.ACCOUNT.DETAILS    = ''
    CALL OPF(FN.AA.ACCOUNT.DETAILS, F.AA.ACCOUNT.DETAILS)
    
    FN.EB.CONTRACT.BALANCES = 'F.EB.CONTRACT.BALANCES'
    F.EB.CONTRACT.BALANCES  = ''
    CALL OPF(FN.EB.CONTRACT.BALANCES, F.EB.CONTRACT.BALANCES)
    
    FN.BTPNS.TH.POOL.NISBAH.DEP = "F.BTPNS.TH.POOL.NISBAH.DEP"
    F.BTPNS.TH.POOL.NISBAH.DEP  = ""
    CALL OPF(FN.BTPNS.TH.POOL.NISBAH.DEP,F.BTPNS.TH.POOL.NISBAH.DEP)

    Y.APP<1>        = 'AA.ARR.ACCOUNT'
    Y.APP<2>        = 'AA.ARR.TERM.AMOUNT'
    Y.FLD.NAME<1>   = 'L.BILYET.NUM' :VM: 'L.FINAL.NISBA' :VM: 'ATI.JOINT.NAME'
    Y.FLD.NAME<2>   = 'L.MAT.MODE' :VM: 'L.ORG.MAT.DATE' 
    
    CALL MULTI.GET.LOC.REF(Y.APP, Y.FLD.NAME, Y.POS)

    Y.L.BILYET.NUM.POS      = Y.POS<1,1>
    Y.L.FINAL.NISBA.POS     = Y.POS<1,2>
    Y.ATI.JOINT.NAME.POS    = Y.POS<1,3>
    Y.L.MAT.MODE.POS        = Y.POS<2,1>
    Y.L.ORG.MAT.DATE.POS    = Y.POS<2,2>

    Y.AA.ID = AA$ARR.ID

    CALL F.READ(FN.AA.ARRANGEMENT,Y.AA.ID,R.AA.ARRANGEMENT,F.AA.ARRANGEMENT,ERR.AA.ARRANGEMENT)
    Y.PRODUCT.LINE      = R.AA.ARRANGEMENT<AA.ARR.PRODUCT.LINE>
    Y.LINKED.APPL.ID    = R.AA.ARRANGEMENT<AA.ARR.LINKED.APPL.ID>
    Y.CO.CODE           = R.AA.ARRANGEMENT<AA.ARR.CO.CODE>
    
    CALL F.READ(FN.EB.CONTRACT.BALANCES, Y.LINKED.APPL.ID, R.EB.CONTRACT.BALANCES, F.EB.CONTRACT.BALANCES, ERR.EB.CONTRACT.BALANCES)
    Y.AMOUNT            = R.EB.CONTRACT.BALANCES<ECB.WORKING.BALANCE>
    
    CALL F.READ(FN.AA.ACCOUNT.DETAILS,Y.AA.ID,R.AA.ACCOUNT.DETAILS,F.AA.ACCOUNT.DETAILS,AA.ACCOUNT.DETAILS.ERR)
    Y.RENEWAL.DATE      = R.AA.ACCOUNT.DETAILS<AA.AD.RENEWAL.DATE>
    
    CALL F.READ(FN.ACCOUNT,Y.LINKED.APPL.ID,REC.ACCOUNT,F.ACCOUNT,ERR.AC)
    Y.ACCOUNT.OFFICER   = REC.ACCOUNT<AC.ACCOUNT.OFFICER>
    
    Y.L.FINAL.NISBA         = R.NEW(AA.AC.LOCAL.REF)<1,Y.L.FINAL.NISBA.POS>
    Y.USER.ID               = R.NEW(AA.AC.INPUTTER)
    Y.AUTHORISER.ID         = TNO:"_":OPERATOR
    Y.DATE.TIME             = R.NEW(AA.AC.DATE.TIME)
    Y.TIME.RECORD           = Y.DATE.TIME[7,2]:":":Y.DATE.TIME[9,2]
    Y.CATEGORY              = R.NEW(AA.AC.CATEGORY)
    Y.COMP.SEQ              = FIELD(R.NEW(AA.AC.ID.COMP.3),".",2)
    Y.EFFECTIVE.DATE        = c_aalocActivityEffDate

    CALL F.READ(FN.AA.ACTIVITY.HISTORY,Y.AA.ID,R.AA.ACTIVITY.HISTORY,F.AA.ACTIVITY.HISTORY,ERR.AA.ACTIVITY.HISTORY)	;*20200320	SAI
	
	FIND Y.EFFECTIVE.DATE IN R.AA.ACTIVITY.HISTORY SETTING Y.POS.FM, Y.POS.VM, Y.POS.SM THEN		
		Y.SYSTEM.DATE.HIS		= R.AA.ACTIVITY.HISTORY<AA.AH.SYSTEM.DATE, Y.POS.VM>
		Y.SM					= DCOUNT(Y.SYSTEM.DATE.HIS,SM) + 1 - Y.COMP.SEQ
		Y.DATE.CHG				= R.AA.ACTIVITY.HISTORY<AA.AH.SYSTEM.DATE,Y.POS.VM,Y.SM>
	END

    idProperty = "ACCOUNT"
    idLinkRef = ""
    CALL AA.PROPERTY.REF(Y.AA.ID, idProperty, idLinkRef)
    IF idLinkRef THEN CALL F.READ(FN.AA.ARR.ACCOUNT, idLinkRef, R.AA.ACCOUNT, F.AA.ARR.ACCOUNT, "")
    Y.BEFORE.NISBA          = R.AA.ACCOUNT<AA.AC.LOCAL.REF,Y.L.FINAL.NISBA.POS>
    Y.L.BILYET.NUM          = R.AA.ACCOUNT<AA.AC.LOCAL.REF,Y.L.BILYET.NUM.POS>
    Y.ATI.JOINT.NAME        = R.AA.ACCOUNT<AA.AC.LOCAL.REF,Y.ATI.JOINT.NAME.POS>


    RETURN
*------------------------------------------------------------------------------
PROCESS:
*------------------------------------------------------------------------------

    Y.ID.STAGING=Y.AA.ID:"*":Y.DATE.CHG:"*":Y.EFFECTIVE.DATE:"*":Y.COMP.SEQ
    CALL F.READ(FN.BTPNS.TH.POOL.NISBAH.DEP,Y.ID.STAGING,R.BTPNS.TH.POOL.NISBAH.DEP,F.BTPNS.TH.POOL.NISBAH.DEP,ERR.BTPNS.TH.POOL.NISBAH.DEP)
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.AA.ID>           = Y.AA.ID 
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.CATEGORY>        = Y.CATEGORY
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.CHANGE.DATE>     = Y.DATE.CHG
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.L.BILYET.NUM>    = Y.L.BILYET.NUM
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.ATI.JOINT.NAME>  = Y.ATI.JOINT.NAME
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.NISBAH.BEFORE>   = Y.BEFORE.NISBA
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.NISBAH.AFTER>    = Y.L.FINAL.NISBA
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.EFFECTIVE.DATE>  = Y.EFFECTIVE.DATE
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.PRINCIPAL>       = Y.AMOUNT
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.USER.INPUT>      = Y.USER.ID
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.USER.AUTHORISER> = Y.AUTHORISER.ID
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.ACCOUNT.OFFICER> = Y.ACCOUNT.OFFICER
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.CO.CODE>         = Y.CO.CODE
        X = OCONV(DATE(),"D-")
        Y.TIME = OCONV(TIME(),"MTS")
        DT = X[9,2]:X[1,2]:X[4,2]:Y.TIME[1,2]:Y.TIME[4,2]:Y.TIME[7,2]
        Y.DATE.TIME = X[9,2]:X[1,2]:X[4,2]:Y.TIME[1,2]:Y.TIME[4,2]
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.TIME.AUTHOR>       = Y.TIME[1,2]:":":Y.TIME[4,2]
        R.BTPNS.TH.POOL.NISBAH.DEP<POOL.NIS.DATE.TIME>         = Y.DATE.TIME

        CALL F.WRITE(FN.BTPNS.TH.POOL.NISBAH.DEP,Y.ID.STAGING,R.BTPNS.TH.POOL.NISBAH.DEP)
    RETURN
*--------------------------------------------------------------------------------
END

