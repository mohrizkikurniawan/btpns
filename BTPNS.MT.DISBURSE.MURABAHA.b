*-----------------------------------------------------------------------------
    SUBROUTINE BTPNS.MT.DISBURSE.MURABAHA(Y.FILE.ID)
*-----------------------------------------------------------------------------
* Developer Name     : Kania Lydia
* Development Date   : 20220816
* Description        : Job untuk memproses Data textfile terkait disburse murabaha
*						BNK/ATI.BM.AGRI.BULK.POST.PROCESS
*-----------------------------------------------------------------------------
* Modification History:-
*-----------------------------------------------------------------------------
* Date            Modified by                Description
*-----------------------------------------------------------------------------
* Developer Name     : Kania Lydia
* Development Date   : 20221108
* Description        : Penambahan PK Number atau Nomor Akad
*-----------------------------------------------------------------------------

    $INSERT I_F.ATI.TH.PRODUCT.PARAMETER
    $INSERT I_COMMON
    $INSERT I_EQUATE
	$INSERT I_F.ACCOUNT
	$INSERT I_F.ATI.TH.LIQ.GENERAL.PARAM
	$INSERT I_F.LIMIT
	$INSERT I_F.CUSTOMER
	$INSERT I_F.IS.MISCASSET
	$INSERT I_F.AA.ARRANGEMENT
	$INSERT I_F.AA.ARRANGEMENT.ACTIVITY
	$INSERT I_F.IS.CONTRACT
	$INSERT I_F.IS.PARAMETER
	$INSERT I_F.FUNDS.TRANSFER
	$INSERT I_F.LIMIT
	$INSERT I_BTPNS.MT.DISBURSE.MURABAHA.COMMON
	
*-----------------------------------------------------------------------------
    GOSUB INIT
	
	CALL F.READ(FN.PROCESS, Y.FILE.ID, R.RECORD, F.PROCESS, ERR.PROCESS)
	
	Y.REC.CNT	= DCOUNT(R.RECORD, FM)
	FOR X = 1 TO Y.REC.CNT
		Y.RECORD	= R.RECORD<X>
		GOSUB PROCESS
	NEXT Y.REC.CNT	
	
	GOSUB FINAL.WRITE
	
    RETURN

*-----------------------------------------------------------------------------
INIT:
*-----------------------------------------------------------------------------
	
	Y.OFS.SOURCE				= 'FINANCING.OFS'
	Y.LI.CLD.FLAG				= 0
	Y.LI.WKL.FLAG				= 0
	Y.LI.PRN.FLAG				= 0
	Y.LIM.CHILD.FLAG			= 0
	Y.PROD.PAR.CATEGORY.PARENT	= ''
	Y.PROD.PAR.CATEGORY.CHILD	= ''
	Y.FLAG						= 1
	Y.FLAG.TYPE					= 1						
	
	FN.ACCOUNT		= "F.ACCOUNT"
	F.ACCOUNT		= ""
	CALL OPF(FN.ACCOUNT, F.ACCOUNT)
	
    RETURN

*-----------------------------------------------------------------------------
PROCESS:
*-----------------------------------------------------------------------------
	
	Y.CIF			= FIELD(Y.RECORD,'|',1)
	Y.ACCOUNT		= FIELD(Y.RECORD,'|',2)
	Y.START.DATE	= FIELD(Y.RECORD,'|',3)
	Y.EXPIRY.DATE	= FIELD(Y.RECORD,'|',4)
	Y.TENOR			= FIELD(Y.RECORD,'|',5)
	Y.AMOUNT		= FIELD(Y.RECORD,'|',6)
	Y.INT.RATE		= FIELD(Y.RECORD,'|',7)
	Y.CODE.PRODUCT	= FIELD(Y.RECORD,'|',8)
	Y.FLAG.REVO		= FIELD(Y.RECORD,'|',9)
	Y.PAY.SCH.TYPE	= FIELD(Y.RECORD,'|',10)
	Y.PK.NUMBER		= FIELD(Y.RECORD,'|',11)
*	Y.PK.NUMBER		= CHANGE(TIMESTAMP(),'.','')
	
		
	CALL F.READ(FN.ATI.TH.PRODUCT.PARAMETER, Y.CODE.PRODUCT, R.PROD.PAR, F.ATI.TH.PRODUCT.PARAMETER, PROD.ERR)
    Y.PROD.WAIVE.FLAG			= R.PROD.PAR<PROD.PAR.AS.PRODUCT>
	Y.PROD.FLAG.REVOLVING		= R.PROD.PAR<PROD.PAR.FLAG.REVOLVING>
	Y.PROD.PAR.WAKALAH			= '000':R.PROD.PAR<PROD.PAR.WAKALAH.LIMIT>	
        
    FINDSTR Y.FLAG.REVO IN Y.PROD.FLAG.REVOLVING SETTING Y.POSF, Y.POSV, Y.POSS THEN
        Y.CATEGORY.PARENT			= R.PROD.PAR<PROD.PAR.CATEGORY.PARENT, Y.POSV>
        Y.CATEGORY.CHILD			= R.PROD.PAR<PROD.PAR.CATEGORY.CHILD, Y.POSV>
		Y.PROD.PAR.CATEGORY.PARENT	= '000':Y.CATEGORY.PARENT	
        Y.PROD.PAR.CATEGORY.CHILD	= '000':Y.CATEGORY.CHILD	
		Y.FLAG	= 0
    END
	
	CALL F.READ(FN.LIMIT.LIABILITY, Y.CIF, R.LIMIT.LIABILITY, F.LIMIT.LIABILITY, ERR.LIMIT.LIABILITY)
	Y.CREDIT.LINE.NO		= R.LIMIT.LIABILITY
	Y.CREDIT.LINE.NO.CNT	= DCOUNT(Y.CREDIT.LINE.NO, FM)
	
	Y.LIMIT.PARENT	= Y.CIF:'.':Y.PROD.PAR.CATEGORY.PARENT:'.01'
	Y.LIMIT.CHILD	= Y.CIF:'.':Y.PROD.PAR.CATEGORY.CHILD:'.01'
	Y.LIMIT.WAKALA	= Y.CIF:'.':Y.PROD.PAR.WAKALAH:'.01'
	
	CALL F.READ(FN.LIMIT, Y.LIMIT.CHILD, R.LIMIT.CHILD, F.LIMIT, ERR.LIMIT)
	Y.AMOUNT.OLD	= R.LIMIT.CHILD<LI.INTERNAL.AMOUNT>
	
	IF Y.AMOUNT.OLD THEN
		Y.AMOUNT.NEW	= Y.AMOUNT + Y.AMOUNT.OLD
	END
	
	CALL F.READ(FN.LIMIT, Y.LIMIT.PARENT, R.LIMIT, F.LIMIT, ERR.LIMIT)
	Y.CO.BOOK	= R.LIMIT<LI.LOCAL.REF, Y.CO.BOOK.POS>
	
	CALL F.READ(FN.CUSTOMER, Y.CIF, R.CUSTOMER, F.CUSTOMER, ERR.CUSTOMER)
	Y.SHORT.NAME	= R.CUSTOMER<EB.CUS.SHORT.NAME>
	Y.CIF.PROJ.LOC	= R.CUSTOMER<EB.CUS.LOCAL.REF, Y.DISTRICT.CODE.POS>
	
	CALL F.READ(FN.IS.PARAMETER, Y.PROD.WAIVE.FLAG, R.IS.PARAMETER, F.IS.PARAMETER, ERR.IS.PARAMETER)
	CALL F.READ(FN.ACCOUNT, Y.ACCOUNT, R.ACCOUNT, F.ACCOUNT, ERR.ACCOUNT)
	
	IF Y.PAY.SCH.TYPE EQ 'BULLET' OR Y.PAY.SCH.TYPE EQ 'ANNUITY' THEN
		Y.FLAG.TYPE	= 0
	END
	
	BEGIN CASE
	CASE R.CUSTOMER EQ ''
		Y.RESPONSE.ERROR = 'MISSING CUSTOMER - RECORD'
		GOSUB OFS.FAILED
	CASE R.LIMIT EQ ''
		Y.RESPONSE.ERROR = 'MISSING LIMIT PARENT - RECORD'
		GOSUB OFS.FAILED
	CASE R.ACCOUNT EQ ''
		Y.RESPONSE.ERROR = 'MISSING ACCOUNT - RECORD'
		GOSUB OFS.FAILED
	CASE Y.CO.BOOK EQ ''
		Y.RESPONSE.ERROR = 'MISSING CO.CODE ON LIMIT PARENT'
		GOSUB OFS.FAILED
	CASE R.PROD.PAR EQ ''
		Y.RESPONSE.ERROR = 'MISSING ':Y.CODE.PRODUCT:' ON PRODUCT PARAMETER'
		GOSUB OFS.FAILED
	CASE Y.FLAG EQ 1
		Y.RESPONSE.ERROR = 'FLAG REVOLVING MUST BE Y/N'
		GOSUB OFS.FAILED
	CASE Y.FLAG.TYPE EQ 1
		Y.RESPONSE.ERROR = 'PAYMENT SCHEDULE TYPE MUST BE BULLET/ANNUITY'
		GOSUB OFS.FAILED
	CASE Y.CIF EQ '' 
		Y.RESPONSE.ERROR = 'CIF CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.ACCOUNT EQ '' 
		Y.RESPONSE.ERROR = 'ACCOUNT CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.START.DATE EQ '' 
		Y.RESPONSE.ERROR = 'START.DATE CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.EXPIRY.DATE EQ '' 
		Y.RESPONSE.ERROR = 'EXPIRY.DATE CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.INT.RATE EQ '' 
		Y.RESPONSE.ERROR = 'RATE CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.TENOR EQ '' 
		Y.RESPONSE.ERROR = 'TENOR CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.AMOUNT EQ ''
		Y.RESPONSE.ERROR = 'AMOUNT CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.CODE.PRODUCT	EQ ''
		Y.RESPONSE.ERROR = 'CODE PRODUCT CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.FLAG.REVO EQ ''
		Y.RESPONSE.ERROR = 'FLAG REVOLVING CANT BE NULL'
		GOSUB OFS.FAILED
	CASE Y.PAY.SCH.TYPE	EQ ''
		Y.RESPONSE.ERROR = 'PAYMENT SCHEDULE TYPE CANT BE NULL BULLET/ANNUITY'
		GOSUB OFS.FAILED
	CASE Y.PK.NUMBER EQ ''	
		Y.RESPONSE.ERROR = 'PK NUMBER/AKAD NUMBER CANT BE NULL'
		GOSUB OFS.FAILED
	CASE 1
		GOSUB DISBURSE.PROCESS
	END CASE

*	IF Y.CO.BOOK EQ '' THEN
*		Y.RESPONSE.ERROR = 'LIMIT CO.BOOK CANT NULL'
*		GOSUB OFS.FAILED
*		RETURN
*	END

*	IF R.CUSTOMER EQ '' THEN
*		Y.RESPONSE.ERROR = 'MISSING CUSTOMER - RECORD'
*		GOSUB OFS.FAILED
*		RETURN
*	END
			
    RETURN
*-----------------------------------------------------------------------------
DISBURSE.PROCESS:
*-----------------------------------------------------------------------------
	
	GOSUB OFS.1		
	
	IF Y.FLAG.OFS.1 EQ '1' THEN
		GOSUB OFS.2
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END
	
	IF Y.FLAG.OFS.2 EQ '1' THEN
		GOSUB OFS.3
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END
	
	IF Y.FLAG.OFS.3 EQ '1' THEN
		GOSUB OFS.4
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END
	
	IF Y.FLAG.OFS.4 EQ '1' THEN
		GOSUB OFS.5
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END
	
	IF Y.FLAG.OFS.5 EQ '1' THEN
		GOSUB OFS.6
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END
	
	IF Y.FLAG.OFS.6 EQ '1' THEN
		GOSUB OFS.7
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END
	
	IF Y.FLAG.OFS.7 EQ '1' THEN
		GOSUB OFS.SUCCESS
	END ELSE
		GOSUB OFS.FAILED
		RETURN
	END	
	
    RETURN
*-----------------------------------------------------------------------------
OFS.1:
*-----------------------------------------------------------------------------

    Y.APP.NAME       = 'LIMIT'
    Y.OFS.FUNCT      = 'I'
    Y.PROCESS        = 'PROCESS'
    Y.OFS.VERSION    = Y.APP.NAME : ',BTPNS.LI.CHILD.OFS'
    Y.GTS.MODE       = ''
    Y.NO.OF.AUTH     = ''
    Y.TRANSACTION.ID = Y.LIMIT.CHILD
    Y.OFS.RESPONSE   = ''
    R.LIM.PAR.OFS    = ''
    Y.FLAG.PROCESS   = ''

	R.LIM.PAR.OFS<LI.ONLINE.LIMIT.DATE>			= Y.START.DATE
	R.LIM.PAR.OFS<LI.PROPOSAL.DATE>				= Y.START.DATE
	R.LIM.PAR.OFS<LI.APPROVAL.DATE>				= Y.START.DATE
	BEGIN CASE
	CASE Y.AMOUNT.OLD NE ''
		R.LIM.PAR.OFS<LI.INTERNAL.AMOUNT>		= Y.AMOUNT.NEW
		R.LIM.PAR.OFS<LI.MAXIMUM.TOTAL>			= Y.AMOUNT.NEW
	CASE 1
		R.LIM.PAR.OFS<LI.INTERNAL.AMOUNT>		= Y.AMOUNT
		R.LIM.PAR.OFS<LI.MAXIMUM.TOTAL>			= Y.AMOUNT
	END CASE 
	R.LIM.PAR.OFS<LI.EXPIRY.DATE>				= Y.EXPIRY.DATE
	R.LIM.PAR.OFS<LI.LOCAL.REF, Y.CO.BOOK.POS>	= Y.CO.BOOK
	R.LIM.PAR.OFS<LI.REVIEW.FREQUENCY>			= 'M1201'
	
	ID.COMPANY	= Y.CO.BOOK
    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.LIM.PAR.OFS, Y.OFS.MESSAGE)
	
    Y.REQUEST.COMMITED	= ''
    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)	
	
    Y.OFS.RESPONSE.HDR	= FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.1		= FIELD(Y.OFS.RESPONSE.HDR,'/',3)	
	Y.RESPONSE.ERROR	= 'CREATE LIMIT CHILD - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)
	
    RETURN
*-----------------------------------------------------------------------------
OFS.2:
*-----------------------------------------------------------------------------

	Y.APP.NAME       = 'LIMIT'
    Y.OFS.FUNCT      = 'I'
    Y.PROCESS        = 'PROCESS'
    Y.OFS.VERSION    = Y.APP.NAME : ',BTPNS.LI.WAKALA.OFS'
    Y.GTS.MODE       = ''
    Y.NO.OF.AUTH     = ''
    Y.TRANSACTION.ID = Y.LIMIT.WAKALA
    Y.OFS.RESPONSE   = ''
    R.LIM.PAR.OFS	 = ''
    Y.FLAG.PROCESS   = ''

	R.LIM.PAR.OFS<LI.ONLINE.LIMIT.DATE>			= Y.START.DATE
	R.LIM.PAR.OFS<LI.PROPOSAL.DATE>				= Y.START.DATE
	R.LIM.PAR.OFS<LI.APPROVAL.DATE>				= Y.START.DATE
	BEGIN CASE
	CASE Y.AMOUNT.OLD NE ''
		R.LIM.PAR.OFS<LI.INTERNAL.AMOUNT>		= Y.AMOUNT.NEW
		R.LIM.PAR.OFS<LI.MAXIMUM.TOTAL>			= Y.AMOUNT.NEW
		R.LIM.PAR.OFS<LI.ONLINE.LIMIT>			= Y.AMOUNT.NEW	
		R.LIM.PAR.OFS<LI.AVAIL.AMT>				= Y.AMOUNT.NEW
	CASE 1
		R.LIM.PAR.OFS<LI.INTERNAL.AMOUNT>		= Y.AMOUNT
		R.LIM.PAR.OFS<LI.MAXIMUM.TOTAL>			= Y.AMOUNT
		R.LIM.PAR.OFS<LI.ONLINE.LIMIT>			= Y.AMOUNT	
		R.LIM.PAR.OFS<LI.AVAIL.AMT>				= Y.AMOUNT
	END CASE 	
	R.LIM.PAR.OFS<LI.EXPIRY.DATE>				= Y.EXPIRY.DATE
	R.LIM.PAR.OFS<LI.OFFERED.UNTIL>				= Y.START.DATE
	R.LIM.PAR.OFS<LI.LOCAL.REF, Y.CO.BOOK.POS>	= Y.CO.BOOK
	R.LIM.PAR.OFS<LI.REVIEW.FREQUENCY>			= 'M1201'
	
	ID.COMPANY	= Y.CO.BOOK
    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.LIM.PAR.OFS, Y.OFS.MESSAGE)
	
    Y.REQUEST.COMMITED	= ''
	
    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)	
	
    Y.OFS.RESPONSE.HDR	= FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.2		= FIELD(Y.OFS.RESPONSE.HDR,'/',3)	
	Y.RESPONSE.ERROR	= 'CREATE LIMIT WAKALA - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)
	
	
	RETURN
*-----------------------------------------------------------------------------
OFS.3:
*-----------------------------------------------------------------------------
	
	
    Y.APP.NAME       = 'AA.ARRANGEMENT.ACTIVITY'
    Y.OFS.FUNCT      = 'I'
    Y.PROCESS        = 'PROCESS'
    Y.OFS.VERSION    = Y.APP.NAME : ',BTPNS.OFS'
    Y.GTS.MODE       = ''
    Y.NO.OF.AUTH     = ''
    Y.TRANSACTION.ID = ''
    Y.OFS.RESPONSE   = ''
    R.AA.ARR.OFS     = ''
    Y.FLAG.PROCESS   = ''	
	
    R.AA.ARR.OFS<AA.ARR.ACT.ARRANGEMENT>     				= 'NEW'
    R.AA.ARR.OFS<AA.ARR.ACT.PRODUCT>         				= 'BTPNS.WAKALA'
    R.AA.ARR.OFS<AA.ARR.ACT.ACTIVITY>        				= 'LENDING-NEW-ARRANGEMENT'
    R.AA.ARR.OFS<AA.ARR.ACT.EFFECTIVE.DATE>  				= Y.START.DATE
    R.AA.ARR.OFS<AA.ARR.ACT.CUSTOMER>        				= Y.CIF
    R.AA.ARR.OFS<AA.ARR.ACT.CURRENCY>        				= 'IDR'
	R.AA.ARR.OFS<AA.ARR.ACT.LOCAL.REF, Y.IS.PRODUCT.POS>	= Y.PROD.WAIVE.FLAG
	
    R.AA.ARR.OFS<AA.ARR.ACT.PROPERTY,1>      				= 'COMMITMENT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,1,1>  				= 'AMOUNT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,1,1> 				= Y.AMOUNT
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,1,2>  				= 'TERM'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,1,2> 				= '1Y'
	
    R.AA.ARR.OFS<AA.ARR.ACT.PROPERTY,2>      				= 'LIMIT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,2,1>  				= 'LIMIT.REFERENCE'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,2,1> 				= '8300'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,2,2>  				= 'LIMIT.SERIAL'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,2,2> 				= '01'
	R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,2,3>  				= 'SINGLE.LIMIT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,2,3> 				= 'N'
	
    R.AA.ARR.OFS<AA.ARR.ACT.PROPERTY,3>      				= 'ACCOUNT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,1>  				= 'L.END.CON.DATE'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,1> 				= Y.EXPIRY.DATE
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,2>  				= 'L.PROJECT.LOC'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,2> 				= Y.CIF.PROJ.LOC
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,3>  				= 'L.TYPE.USE'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,3> 				= '1'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,4>  				= 'L.ECON.SEC'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,4> 				= '930000'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,5>  				= 'ACCOUNT.TITLE.1'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,5> 				= Y.SHORT.NAME	
	R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,6>  				= 'PK.NUMBER'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,6> 				= Y.PK.NUMBER
	R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,3,7>  				= 'LAST.PK.NUMBER'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,3,7> 				= Y.PK.NUMBER
	
    R.AA.ARR.OFS<AA.ARR.ACT.PROPERTY,4>      				= 'SETTLEMENT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,4,1>  				= 'PAYOUT.ACCOUNT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,4,1> 				= Y.ACCOUNT
	R.AA.ARR.OFS<AA.ARR.ACT.FIELD.NAME,4,2>  				= 'PAYIN.ACCOUNT'
    R.AA.ARR.OFS<AA.ARR.ACT.FIELD.VALUE,4,2> 				= 'IDR' : R.IS.PARAMETER<IS.PAR.MULTI.SUPP.CAT> : '0001' : Y.CO.BOOK[4]
	
    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.AA.ARR.OFS, Y.OFS.MESSAGE)

    Y.REQUEST.COMMITED = ''
    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)

    Y.OFS.RESPONSE.HDR	= FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.3		= FIELD(Y.OFS.RESPONSE.HDR,'/',3)
	Y.VARIABLE.U		= FIELD(Y.OFS.RESPONSE, ',', '2')
	Y.AAA.WAKALAH.ID	= FIELD(Y.VARIABLE.U, '=', '2')	
	Y.RESPONSE.ERROR	= 'CREATE KONTRAK WAKALA - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)	
	Y.RESPONSE.ERROR	= FIELD(Y.RESPONSE.ERROR, '-1/', 2)
	Y.RESPONSE.ERROR	= FIELD(Y.RESPONSE.ERROR, '</', 1)
	Y.RESPONSE.ERROR	= FIELD(Y.RESPONSE.ERROR, ',', 2)
	
    RETURN
    
*-----------------------------------------------------------------------------
OFS.4:
*-----------------------------------------------------------------------------
	Y.APP.NAME       	= 'IS.MISCASSET'
    Y.OFS.FUNCT      	= 'I'
    Y.PROCESS        	= 'PROCESS'
    Y.OFS.VERSION    	= Y.APP.NAME : ',BTPNS.OFS'
    Y.GTS.MODE       	= ''
    Y.NO.OF.AUTH     	= ''
    Y.TRANSACTION.ID 	= ''
    Y.OFS.RESPONSE   	= ''
    R.IS.MISCASSET.OFS  = ''
    Y.FLAG.PROCESS   	= ''

	R.IS.MISCASSET.OFS<IS.MIS12.CUSTOMER>		= Y.CIF
	R.IS.MISCASSET.OFS<IS.MIS12.ASSET.DESC>		= 'MISCASSET'
	R.IS.MISCASSET.OFS<IS.MIS12.CO.CODE>		= Y.CO.BOOK
	R.IS.MISCASSET.OFS<IS.MIS12.UNIT.PRICE>		= Y.AMOUNT
	R.IS.MISCASSET.OFS<IS.MIS12.VENDOR>			= '999999'
	R.IS.MISCASSET.OFS<IS.MIS12.CURRENCY>		= 'IDR'

    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.IS.MISCASSET.OFS, Y.OFS.MESSAGE)

    Y.REQUEST.COMMITED = ''
    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)
	
	Y.OFS.RESPONSE.HDR  = FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.4        = FIELD(Y.OFS.RESPONSE.HDR,'/',3)	
	Y.IS.MISCASSET.ID	= FIELD(Y.OFS.RESPONSE, '/', 1)		
	Y.RESPONSE.ERROR	= 'CREATE MISCASSET - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)
	
    RETURN
	
*-----------------------------------------------------------------------------
OFS.5:
*-----------------------------------------------------------------------------

    Y.APP.NAME       = 'IS.CONTRACT'
    Y.OFS.FUNCT      = 'I'
    Y.PROCESS        = 'PROCESS'
    Y.OFS.VERSION    = Y.APP.NAME : ',BTPNS.OFS'
    Y.GTS.MODE       = ''
    Y.NO.OF.AUTH     = ''
    Y.TRANSACTION.ID = ''
    Y.OFS.RESPONSE   = ''
    R.IS.CONTRACT.OFS  = ''
    Y.FLAG.PROCESS   = ''

	R.IS.CONTRACT.OFS<IS.CON.CO.CODE>			= Y.CO.BOOK
	R.IS.CONTRACT.OFS<IS.CON.CUSTOMER>			= Y.CIF
	R.IS.CONTRACT.OFS<IS.CON.PRODUCT>			= Y.PROD.WAIVE.FLAG
	R.IS.CONTRACT.OFS<IS.CON.DESCRIPTION>		= 'MISCASSET'
	R.IS.CONTRACT.OFS<IS.CON.STATUS.VALUE.DATE>	= Y.START.DATE				
	R.IS.CONTRACT.OFS<IS.CON.QUANTITY>          = '1'
	R.IS.CONTRACT.OFS<IS.CON.VALUE.DATE>		= Y.START.DATE
	R.IS.CONTRACT.OFS<IS.CON.ASSET.REF>			= Y.IS.MISCASSET.ID
	R.IS.CONTRACT.OFS<IS.CON.VENDOR>			= '999999'
	R.IS.CONTRACT.OFS<IS.CON.VENDOR.NAME>		= 'Bank BTPN Syariah'
	R.IS.CONTRACT.OFS<IS.CON.WAKALA.REF>		= Y.AAA.WAKALAH.ID
	R.IS.CONTRACT.OFS<IS.CON.CUSTOMER.ACCT>		= Y.ACCOUNT
	R.IS.CONTRACT.OFS<IS.CON.UNIT.PRICE>        = Y.AMOUNT

    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.IS.CONTRACT.OFS, Y.OFS.MESSAGE)
	
    Y.REQUEST.COMMITED = ''
    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)	
	
	Y.OFS.RESPONSE.HDR  = FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.5        = FIELD(Y.OFS.RESPONSE.HDR,'/',3)	
	Y.IS.CONTRACT.ID	= FIELD(Y.OFS.RESPONSE, '/', 1)		
	Y.RESPONSE.ERROR	= 'CREATE IS CONTRACT - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)
	
    RETURN
*-----------------------------------------------------------------------------
OFS.6:
*-----------------------------------------------------------------------------
	
	Y.APP.NAME       	  = 'FUNDS.TRANSFER'
    Y.OFS.FUNCT      	  = 'I'
    Y.PROCESS        	  = 'PROCESS'
    Y.OFS.VERSION    	  = Y.APP.NAME : ',BTPNS.INDV.OFS'
    Y.GTS.MODE       	  = ''
    Y.NO.OF.AUTH     	  = ''
    Y.TRANSACTION.ID 	  = ''
    Y.OFS.RESPONSE   	  = ''
    R.FUNDS.TRANSFER.OFS  = ''
    Y.FLAG.PROCESS        = ''

	R.FUNDS.TRANSFER.OFS<FT.DEBIT.ACCT.NO>		= 'IDR' : R.IS.PARAMETER<IS.PAR.MULTI.SUPP.CAT> : '0001' : Y.CO.BOOK[4]
	R.FUNDS.TRANSFER.OFS<FT.DEBIT.VALUE.DATE>	= Y.START.DATE
	R.FUNDS.TRANSFER.OFS<FT.CREDIT.ACCT.NO>		= Y.AAA.WAKALAH.ID
	R.FUNDS.TRANSFER.OFS<FT.CREDIT.CURRENCY>	= 'IDR'
	R.FUNDS.TRANSFER.OFS<FT.CREDIT.AMOUNT>		= Y.AMOUNT
	R.FUNDS.TRANSFER.OFS<FT.CREDIT.VALUE.DATE>	= Y.START.DATE

    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.FUNDS.TRANSFER.OFS, Y.OFS.MESSAGE)

    Y.REQUEST.COMMITED = ''
    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)
	
	Y.OFS.RESPONSE.HDR  = FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.6        = FIELD(Y.OFS.RESPONSE.HDR,'/',3)	
	Y.FT.WAKALAH.ID		= FIELD(Y.OFS.RESPONSE, '/', 1)	
	Y.RESPONSE.ERROR	= 'CREATE FT PELUNASAN - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)
	Y.RESPONSE.ERROR	= FIELD(Y.RESPONSE.ERROR, '-1/', 2)
	Y.RESPONSE.ERROR	= FIELD(Y.RESPONSE.ERROR, '</', 1)
	Y.RESPONSE.ERROR	= FIELD(Y.RESPONSE.ERROR, ',', 2)	
    
	RETURN

*-----------------------------------------------------------------------------
OFS.7:
*-----------------------------------------------------------------------------

	Y.APP.NAME       = 'AA.ARRANGEMENT.ACTIVITY'
    Y.OFS.FUNCT      = 'I'
    Y.PROCESS        = 'PROCESS'
    Y.OFS.VERSION    = Y.APP.NAME : ',BTPNS.INDV.OFS'
    Y.GTS.MODE       = ''
    Y.NO.OF.AUTH     = ''
    Y.TRANSACTION.ID = ''
    Y.OFS.RESPONSE   = ''
    R.AA.ARR.OFS.1   = ''
    Y.FLAG.PROCESS   = ''	
	
    R.AA.ARR.OFS.1<AA.ARR.ACT.ARRANGEMENT>     				  	= 'NEW'
    R.AA.ARR.OFS.1<AA.ARR.ACT.PRODUCT>         				  	= 'BTPNS.MURABAHA'
    R.AA.ARR.OFS.1<AA.ARR.ACT.ACTIVITY>        				  	= 'LENDING-NEW-ARRANGEMENT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.EFFECTIVE.DATE>  				  	= Y.START.DATE
    R.AA.ARR.OFS.1<AA.ARR.ACT.CUSTOMER>        				  	= Y.CIF
    R.AA.ARR.OFS.1<AA.ARR.ACT.CURRENCY>        				  	= 'IDR'
	R.AA.ARR.OFS.1<AA.ARR.ACT.LOCAL.REF, Y.IS.PRODUCT.POS>	  	= Y.PROD.WAIVE.FLAG
	R.AA.ARR.OFS.1<AA.ARR.ACT.LOCAL.REF, Y.IS.CONTRACT.REF.POS>	= Y.IS.CONTRACT.ID	
	
    R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,1>      		= 'COMMITMENT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,1,1>  		= 'AMOUNT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,1,1> 		= Y.AMOUNT
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,1,2>  		= 'TERM'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,1,2> 		= Y.TENOR
		
    R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,2>      		= 'LIMIT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,2,1>  		= 'LIMIT.REFERENCE'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,2,1> 		= Y.CATEGORY.CHILD
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,2,2>  		= 'LIMIT.SERIAL'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,2,2> 		= '01'
		
    R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,3>      		= 'ACCOUNT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,1>  		= 'L.END.CON.DATE'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,1> 		= Y.EXPIRY.DATE
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,2>  		= 'L.PROJECT.LOC'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,2> 		= Y.CIF.PROJ.LOC
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,3>  		= 'L.TYPE.USE'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,3> 		= '1'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,4>  		= 'L.ECON.SEC'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,4> 		= '930000'
	R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,5>  		= 'PK.NUMBER'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,5> 		= Y.PK.NUMBER
	R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,6>  		= 'LAST.PK.NUMBER'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,6> 		= Y.PK.NUMBER
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,7>  		= 'L.BEG.CON.DATE'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,7> 		= Y.START.DATE	
	R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,8>  		= 'L.RECV.CHAR'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,8> 		= '9'	
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,9>  		= 'L.PORT.CAT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,9> 		= '14'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,10>  	= 'L.TYPE.RECV'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,10> 	= '10'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,11>  	= 'L.APPR.AGENCY'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,11> 	= '00'	
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,12>  	= 'L.APPR.VALUE'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,12>		= '9999'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,3,13>		= 'ACCOUNT.TITLE.1'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,3,13>		= Y.SHORT.NAME	
		
    R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,4>			= 'PRINCIPALPFT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,4,1>		= 'FIXED.RATE'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,4,1>		= Y.INT.RATE
	
    R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,5>			= 'SETTLEMENT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,5,1>		= 'PAYOUT.ACCOUNT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,5,1>		= 'IDR' : R.IS.PARAMETER<IS.PAR.MULTI.SUPP.CAT> : '0001' : Y.CO.BOOK[4]
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,5,2>  		= 'PAYIN.ACCOUNT'
    R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,5,2> 		= Y.ACCOUNT
	
	BEGIN CASE
	CASE Y.PAY.SCH.TYPE EQ 'BULLET'
		R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,6>			= 'SCHEDULE'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,1>  		= 'PAYMENT.TYPE:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,1> 		= 'DISBURSEMENT.%'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,2>  		= 'PAYMENT.METHOD:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,2> 		= 'PAY'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,3>  		= 'PROPERTY:1:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,3> 		= 'ACCOUNT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,4>  		= 'PERCENTAGE:1:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,4> 		= '100.00'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,5>  		= 'BILL.TYPE:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,5> 		= 'DISBURSEMENT'
		
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,6>  		= 'PAYMENT.TYPE:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,6> 		= 'LINEAR'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,7>  		= 'PAYMENT.METHOD:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,7> 		= 'DUE'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,8>  		= 'PAYMENT.FREQ:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,8> 		= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,9>  		= 'PROPERTY:2:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,9> 		= 'ACCOUNT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,10>  	= 'DUE.FREQ:2:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,10> 	= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,11>  	= 'BILL.TYPE:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,11> 	= 'INSTALLMENT'
	
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,12>  	= 'PAYMENT.TYPE:3'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,12> 	= 'INTEREST'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,13>  	= 'PAYMENT.METHOD:3'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,13> 	= 'DUE'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,14>  	= 'PAYMENT.FREQ:3'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,14> 	= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,15>  	= 'PROPERTY:3:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,15> 	= 'PRINCIPALPFT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,16>  	= 'DUE.FREQ:3:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,16> 	= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,17>  	= 'BILL.TYPE:3'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,17> 	= 'INSTALLMENT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,18>  	= 'BILLS.COMBINED'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,18> 	= 'YES'

	CASE Y.PAY.SCH.TYPE EQ 'ANNUITY'
		R.AA.ARR.OFS.1<AA.ARR.ACT.PROPERTY,6>			= 'SCHEDULE'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,1>  		= 'PAYMENT.TYPE:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,1> 		= 'DISBURSEMENT.%'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,2>  		= 'PAYMENT.METHOD:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,2> 		= 'PAY'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,3>  		= 'PROPERTY:1:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,3> 		= 'ACCOUNT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,4>  		= 'PERCENTAGE:1:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,4> 		= '100.00'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,5>  		= 'BILL.TYPE:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,5> 		= 'DISBURSEMENT'
		
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,6>  		= 'PAYMENT.TYPE:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,6> 		= 'CONSTANT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,7>  		= 'PAYMENT.METHOD:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,7> 		= 'DUE'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,8>  		= 'PAYMENT.FREQ:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,8> 		= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,9>  		= 'PROPERTY:2:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,9> 		= 'ACCOUNT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,10>  	= 'DUE.FREQ:2:1'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,10> 	= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,11>  	= 'PROPERTY:2:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,11> 	= 'PRINCIPALPFT'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,12>  	= 'DUE.FREQ:2:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,12> 	= 'e0Y e1M e0W e0D e0F'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,13>  	= 'BILL.TYPE:2'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,13> 	= 'INSTALLMENT'

		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.NAME,6,14>  	= 'BILLS.COMBINED'
		R.AA.ARR.OFS.1<AA.ARR.ACT.FIELD.VALUE,6,14> 	= 'YES'
	CASE 1 
		Y.RESPONSE.ERROR = 'WRONG PAYMENT SCHEDULE TYPE'
		GOSUB OFS.FAILED
		RETURN
	END CASE
	
	
    CALL OFS.BUILD.RECORD(Y.APP.NAME, Y.OFS.FUNCT, Y.PROCESS, Y.OFS.VERSION, Y.GTS.MODE, Y.NO.OF.AUTH, Y.TRANSACTION.ID, R.AA.ARR.OFS.1, Y.OFS.MESSAGE)
    Y.REQUEST.COMMITED = ''

    CALL OFS.CALL.BULK.MANAGER(Y.OFS.SOURCE,Y.OFS.MESSAGE,Y.OFS.RESPONSE,Y.REQUEST.COMMITED)	

    Y.OFS.RESPONSE.HDR		= FIELD(Y.OFS.RESPONSE,',',1)
    Y.FLAG.OFS.7			= FIELD(Y.OFS.RESPONSE.HDR,'/',3)
	Y.AAA.LOAN.ID.AUTH		= FIELD(Y.OFS.RESPONSE, '/', 1)
	Y.AAA.LOAN.ID			= FIELD(Y.OFS.RESPONSE, ',', '2')
	Y.AAA.LOAN.ID			= FIELD(Y.AAA.LOAN.ID, '=', '2')
	Y.RESPONSE.ERROR		= 'CREATE AA LOAN - ':FIELD(Y.OFS.RESPONSE,Y.OFS.RESPONSE.HDR:',',2)
	Y.RESPONSE.ERROR		= FIELD(Y.RESPONSE.ERROR, '-1/', 2)
	Y.RESPONSE.ERROR		= FIELD(Y.RESPONSE.ERROR, '</', 1)
	Y.RESPONSE.ERROR		= FIELD(Y.RESPONSE.ERROR, ',', 2)
	
	
    RETURN

*-----------------------------------------------------------------------------
OFS.FAILED:
*-----------------------------------------------------------------------------

	Y.DATA.FINAL<-1>	= Y.RECORD:'|ERROR|':Y.RESPONSE.ERROR	
	
    RETURN
*-----------------------------------------------------------------------------
OFS.SUCCESS:
*-----------------------------------------------------------------------------

	Y.DATA.FINAL<-1>	= Y.RECORD:'|SUCCESS|':Y.AAA.LOAN.ID

    RETURN
	
*-----------------------------------------------------------------------------
FINAL.WRITE:
*-----------------------------------------------------------------------------
	
	Y.PROC.ID		= Y.FILE.ID:'.res'
	Y.FILE.DUP		= Y.FILE.ID:'.DISB.INDV.FINACING'
	Y.FILE.BACKUP	= Y.FILE.ID:'.bkp'
	
*---WRITE NEW PROCESS FILE---
	WRITE Y.DATA.FINAL TO F.RESULT, Y.PROC.ID
	
*---WRITE FILE NAME TO TABLE DUPLICATE---	
	WRITE Y.FILE.ID TO F.BTPNS.TT.DUP.UPLOAD.FILE,Y.FILE.DUP 
	
*---WRITE BACKUP FILE---	
	WRITE R.RECORD TO F.BACKUP,Y.FILE.BACKUP

*---DELETE FILE---
	DELETE F.PROCESS, Y.FILE.ID
	
	RETURN

*-----------------------------------------------------------------------------
END
